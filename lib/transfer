FUNCTION TR_RETROGRADE
{
    //RETURN -1.0 * SHIP:VELOCITY:ORBIT:NORMALIZED.
    RETURN HEADING( 270, 0 ).
}

FUNCTION TRANSFER_BURN
{
    PARAMETER TARGET_BODY.
    PARAMETER TARGET_ANGLE.
    PARAMETER TILT.
    PARAMETER TARGET_HEIGHT.
    
    ML( "Coasting to " + TARGET_BODY:NAME + " transfer." ).
        
    SET TRANSFER_DONE TO 0.    
    UNTIL TRANSFER_DONE > 0
    {
        WAIT 1.
        SET MUN_POSITION TO KERBIN:GEOPOSITIONOF( MUN:POSITION ).
        SET SHIP_POSITION TO KERBIN:GEOPOSITIONOF( SHIP:POSITION ).
        SET LONG_DIFF TO SHIP_POSITION:LNG - MUN_POSITION:LNG.
        IF( LONG_DIFF < 0 )
        {
            SET LONG_DIFF TO LONG_DIFF + 360.
        }
        
        IF ( LONG_DIFF > ( TARGET_ANGLE - 5.25 ) )
            AND ( LONG_DIFF < ( TARGET_ANGLE + 5.25 ) )
        {
            SET WARP TO 0.
        }
        ELSE
        {
            SET WARP TO 3.
        }
        
        IF ( LONG_DIFF > ( TARGET_ANGLE - 0.2 ) )
            AND ( LONG_DIFF < ( TARGET_ANGLE + 0.2 ) )
        {
            SET TRANSFER_DONE TO 1.
        }
    }

    ML( "Executing " + TARGET_BODY:NAME + " intercept burn." ).
    
    HANDLE_SIMPLE_STAGE( 1, 0, TILT ).

    WAIT UNTIL SHIP:ORBIT:HASNEXTPATCH.
    
    ML( TARGET_BODY:NAME + " intercept reached." ).
    
    ML( "Minimising encounter periapsis." ).
    
    HANDLE_SIMPLE_STAGE( 0.025, 0, TILT ).
    
    WAIT 0.1.
    
    SET NEW_P TO SHIP:ORBIT:NEXTPATCH:PERIAPSIS.
    SET OLD_P TO SHIP:ORBIT:NEXTPATCH:PERIAPSIS.

    UNTIL( ( OLD_P < NEW_P ) OR ( NEW_P < TARGET_HEIGHT ) )
    {
        SET OLD_P TO NEW_P.
        SET NEW_P TO SHIP:ORBIT:NEXTPATCH:PERIAPSIS.
    }
    
    ML( "Closest approach: " + ROUND( SHIP:ORBIT:NEXTPATCH:PERIAPSIS, 0 ) + "m" ).
    
    LOCK THROTTLE TO 0.
    
    SET WARP TO 3.
    WAIT UNTIL SHIP:ALTITUDE > 150000.
    SET WARP TO 4.
    WAIT UNTIL SHIP:ALTITUDE > 500000.
    SET WARP TO 5.
    WAIT UNTIL SHIP:ORBIT:NEXTPATCHETA < 4000.
    SET WARP TO 4.
    WAIT UNTIL SHIP:ORBIT:NEXTPATCHETA < 500.
    SET WARP TO 3.
       
    WAIT UNTIL SHIP:ORBIT:BODY:NAME = "Mun".
    
    WAIT 5.
    
    SET WARP TO 5.
    WAIT UNTIL ETA:PERIAPSIS < 5000.
    SET WARP TO 4.
    WAIT UNTIL ETA:PERIAPSIS < 700.
    SET WARP TO 3.
    WAIT UNTIL ETA:PERIAPSIS < 100.
    
    SET WARP TO 0.
    
    LOCK STEERING TO TR_RETROGRADE().
    
    WAIT UNTIL ETA:PERIAPSIS < 3.
    
    ML( "Commencing " + TARGET_BODY:NAME + " orbital insertion." ).
    
    LOCK STEERING TO TR_RETROGRADE().
    LOCK THROTTLE TO 1.    
    WAIT UNTIL SHIP:ORBIT:HASNEXTPATCH = FALSE.
    
    LOCK STEERING TO TR_RETROGRADE().
    LOCK THROTTLE TO 1.
    WAIT UNTIL ( SHIP:PERIAPSIS < TARGET_HEIGHT * 1.005 ) AND ( SHIP:APOAPSIS < TARGET_HEIGHT * 1.005 ).
    
    LOCK THROTTLE TO 0.
    
    ML( "Transfer subroutine complete: " + TARGET_BODY:NAME + " orbit reached." ).
    
    LOG_ORBIT( TARGET_HEIGHT ).
}
