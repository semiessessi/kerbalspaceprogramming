RUN ONCE "1:/lib/log".

PRINT( "===========================================" ).
PRINT( "       Mercury Launch Script v0.5.0        " ).
PRINT( "===========================================" ).

FUNCTION STAGE_SETUP
{
    WHEN STAGE:SOLIDFUEL <= 0 AND STAGE:LIQUIDFUEL <= 0 THEN
    {
        ML( "Staging." ).
        STAGE.
        
        STAGE_SETUP.
    }
}

FUNCTION GRAVITY_TURN_ANGLE
{
    SET RAWLERP TO ( ( SHIP:APOAPSIS - 22000 ) / 44000 ).
    SET LERP TO MIN( MAX( RAWLERP, 0.0 ), 1.0 ).
    
    RETURN 80 * ( 1.0 - LERP ).
}

FUNCTION GRAVITY_TURN_THROTTLE
{
    SET RAWLERP TO ( ( SHIP:APOAPSIS - 22000 ) / 44000 ).
    SET LERP TO MIN( MAX( RAWLERP, 0.0 ), 1.0 ).
    
    RETURN 0.8 + 0.2 * LERP.
}

FUNCTION LAUNCH
{
    PARAMETER HEIGHT.
    PARAMETER ANGLE.
    
    IF( ANGLE < 270 )
    {
        SET ANGLE TO ANGLE + 90.
    }
    ELSE
    { 
        SET ANGLE TO ANGLE - 270.
    }
    
    LOCK THROTTLE TO 1.
    
    MLT( -4, "Starting countdown..." ).

    WAIT 1.

    MLT( -3, "3" ).
    WAIT 1.

    MLT( -2, "2" ).
    WAIT 1.

    MLT( -1, "1" ).
    WAIT 1.

    ML( "Ignition." ).

    STAGE.
    
    STAGE_SETUP.
    
    WAIT UNTIL SHIP:VELOCITY:SURFACE:MAG > 1.0.
    
    ML( "We have lift off!" ).
    
    ML( "Steering locked to near vertical." ).
    
    LOCK STEERING TO HEADING( ANGLE, 89.8 ).
    STAGE_SETUP.
    
    WAIT UNTIL SHIP:VELOCITY:SURFACE:MAG > 200.
    
    ML( "Adjusting angle slightly." ).
    ML( "Easing off throttle." ).
    
    LOCK STEERING TO HEADING( ANGLE, 88 ).
    LOCK THROTTLE TO 0.5.
    STAGE_SETUP.
    
    WAIT UNTIL SHIP:ALTITUDE > 10000.
    
    ML( "Commencing gravity turn to reach orbital altitude" ).
    
    LOCK STEERING TO HEADING( ANGLE, GRAVITY_TURN_ANGLE() ).
    LOCK THROTTLE TO GRAVITY_TURN_THROTTLE().
    STAGE_SETUP.
        
    WAIT UNTIL SHIP:APOAPSIS > ( HEIGHT * 1.01 ).
    
    ML( "Coasting to altitude..." ).
    
    LOCK THROTTLE TO 0.0.
    STAGE_SETUP.
    
    WAIT UNTIL SHIP:ALTITUDE > HEIGHT * 0.97.

    ML( "Executing orbital insertion burn." ).
    
    LOCK STEERING TO HEADING( ANGLE, 340 ).
    LOCK THROTTLE TO 1.0.
    STAGE_SETUP.
    
    WAIT UNTIL SHIP:PERIAPSIS > HEIGHT * 0.98.
    
    LOCK THROTTLE TO 0.

    ML( "Launch subroutine complete: Orbit reached." ).
}
